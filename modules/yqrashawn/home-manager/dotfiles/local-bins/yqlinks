#!/usr/bin/env bb

(ns yqlinks
  (:require
   [babashka.curl :as curl]
   [clojure.java.shell :as shell]
   [clojure.string :as s]
   [clojure.tools.cli :refer [parse-opts]]))

(def http-uri-re #"((([A-Za-z]{3,9}:(?:\/\/)?)(?:[\-;:&=\+\$,\w]+@)?[A-Za-z0-9\.\-]+|(?:www\.|[\-;:&=\+\$,\w]+@)[A-Za-z0-9\.\-]+)((?:\/[\+~%\/\.\w\-_]*)?\??(?:[\-\+=&;%@\.\w_]*)#?(?:[\.\!\/\\\w]*))?)")
;; (def http-uri-re #"http[s]?://(?:[a-zA-Z]|[0-9]|[$-_@.&+]|[!*\(\),]|(?:%[0-9a-fA-F][0-9a-fA-F]))+")
;; (defonce http-uri-re #"https?:\/\/(www\.)?[-a-zA-Z0-9@:%._\+~#=]{1,256}\.[a-zA-Z0-9()]{1,6}\b([-a-zA-Z0-9()@:%_\+.~#?&//=]*)")

(defn get-html [url]
  (let [res
        (curl/get url {:headers {"User-Agent"      "Mozilla/5.0 (Macintosh; Intel Mac OS X 10.15; rv:102.0) Gecko/20100101 Firefox/102.0"
                                 "Accept"          "text/html,application/xhtml+xml,application/xml;q=0.9,image/avif,image/webp,*/*;q=0.8"
                                 "Accept-Language" "en-US,en;q=0.7,zh-CN;q=0.3"}})
        {:keys [status body]} res]
    body))

(defn html->title [html]
  (-> (re-seq #"<title.*>(?<title>.*)</title>" html)
      first
      second
      s/trim))

(defn org-link [title url]
  (format "[[%s][%s]]" url title))

(defn md-link [title url]
  (format "[%s](%s)" title url))

(def cli-opts
  [["-t" "--type TYPE" "Output link type"
    :default :md
    :parse-fn keyword
    :validate [(fn [type] (some? (#{:md :org} type))) "Must be one of md or org"]]
   ["-u" "--url URL" "Input url"]])

(defn- main []
  (let [{:keys [options]}  (parse-opts *command-line-args* cli-opts)
        {:keys [url type]} options
        ->link             (if (= type :md) md-link org-link)
        url                (or url (s/trim-newline (:out (shell/sh "pbpaste"))))
        url                (first (re-find http-uri-re url))]
    (when (and url type)
      (try
        (-> url
            get-html
            html->title
            (->link url)
            println)
        (catch Exception e
          (println e)))
      ;; (System/exit 0)
      )))

(main)
