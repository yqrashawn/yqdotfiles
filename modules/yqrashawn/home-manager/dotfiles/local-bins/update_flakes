#!/usr/bin/env bb

(ns update-flakes
  (:require
   [babashka.fs :as fs]
   [babashka.process :as p]
   [cheshire.core :as json]
   [clojure.string :as string]))

(def flakes-skip-auto-updates
  #{:emacs-overlay
    :nixpkgs
    :nixpkgs-unstable
    :emacs-custom-src})

(def dir
  (if (System/getenv "CI")
    (fs/cwd)
    (fs/expand-home "~/.nixpkgs")))

(def all-inputs
  (-> (p/sh {:dir dir} "nix flake metadata --json")
      :out
      (json/parse-string keyword)
      :locks
      :nodes
      :root
      :inputs
      keys))

(def filtered-inputs
  (->> all-inputs
       (filter #(not (flakes-skip-auto-updates %)))
       (map name)))

(def cmd
  (->> filtered-inputs
       (string/join " ")
       (str
        (if (System/getenv "CI")
          "nix flake update --commit-lock-file "
          "nix flake update "))))

(println "Upgrade following inputs:")
(println (->> filtered-inputs
              (map #(str "  " %))
              (string/join "\n")))

(println (str "dir: " dir))
(println (str "run: " cmd))
(p/shell {:dir dir} cmd)
